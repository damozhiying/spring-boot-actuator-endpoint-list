buildscript {
    ext {
        springBootVersion = '1.2.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

version = '0.1.4'

apply plugin: 'java'
apply plugin: 'idea'

jar {
    baseName = 'spring-boot-actuator-endpoint-list'
    version = project.version
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}


dependencies {
    compile("org.springframework.boot:spring-boot-starter-freemarker:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

apply plugin: 'signing'
apply plugin: 'maven'

group = 'eu.hinsch'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'spring-boot-actuator-endpoint-list'
                packaging 'jar'
                description 'simple spring boot actuator entry point to list all endpoints'
                url 'https://github.com/lukashinsch/spring-boot-actuator-endpoint.list/'

                scm {
                    url 'scm:git@github.com:lukashinsch/spring-boot-actuator-endpoint-list.git'
                    connection 'scm:git@github.com:lukashinsch/spring-boot-actuator-endpoint-list.git'
                    developerConnection 'scm:git@github.com:lukashinsch/spring-boot-actuator-endpoint-list.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lukashinsch'
                        name 'Lukas Hinsch'
                    }
                }
            }
        }
    }
}